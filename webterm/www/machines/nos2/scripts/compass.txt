<p>
Use the NEW and TEXT commands to create a Cyber assembly language source file, use MODIFY to create a program library
from it, and use MODIFY again to expand the source and insert system interface definitions. Then use the COMPASS
command to assemble the expanded source, and then run the executable file produced.
</p>
<pre>
     /NEW,SOURCE
     /TEXT
       <i>Enter program source code
       Exit TEXT mode by pressing Control-T key</i>
     /MODIFY,P=0,N=NPL,C=0,F,LO=E,Z./*CREATE SOURCE
     /ATTACH,OPL=OPL871/UN=INSTALL
     /MODIFY,P=NPL,LO=E,Z./*OPLFILE OPL/*EDIT FIB
     /COMPASS,I,L=0
     /LGO
</pre>
<p>References:
 <ul>
  <li>
   <a href="http://bitsavers.trailing-edge.com/pdf/cdc/Tom_Hunter_Scans/COMPASS_v3_Reference_Manual.60492600.1979_.pdf" target="_blank">
   COMPASS Version 3 Reference Manual
   </a>
  </li>
  <li>
   <a href="http://www.bitsavers.org/pdf/cdc/Tom_Hunter_Scans/Modify_RefMan_Sept85.pdf" target="_blank">
   MODIFY Reference Manual
   </a>
  <li>
   <a href="http://bitsavers.trailing-edge.com/pdf/cdc/Tom_Hunter_Scans/NOS_2_Reference_Set_Vol_3_System_Commands_60459680L_Dec88.pdf" target="_blank">
   NOS 2 Reference Set Volume 3 System Commands
   </a>
  </li>
 </ul>
</p>
~~~~
Lnew,source
W^/$
Ltext
W ENTER TEXT MODE
W\\r
I250 \\r =====
FIB
          IDENT  FIB
          ENTRY  FIB
          SYSCOM B1

*CALLC COMCCDD

 MAX      EQU    10D         COUNT OF FIBONACCI NUMBERS TO CALCULATE
 OBUFL    EQU    301B        OUTPUT BUFFER LENGTH

**        MAIN PROGRAM
*
*         CALCULATE AND PRINT THE FIRST 10 FIBONACCI NUMBERS

 FIB      SB1    1

 FIB1     SA1    N
          SB2    MAX
          SB3    X1
          GE     B3,B2,FIB2  IF DONE
          SX1    X1+B1       UPDATE CURRENT INDEX
          BX6    X1
          SA6    A1
          RJ     CDD         CONVERT TO DECIMAL DIGITS
          LX6    6           ENSURE THAT THERE'S A BLANK TO THE RIGHT
          WRITEO O           AND OUTPUT
          SA1    I2          OUTPUT CORRESPONDING FIBONACCI NUMBER
          SA2    I1
          BX6    X1
          IX7    X1+X2       CALCULATE NEXT FIBONACCI NUMBER
          SA6    I1
          SA7    I2
          RJ     CDD         CONVERT CURRENT ONE TO DECIMAL
          BX6    X4          USE THE LEFT-JUSTIFIED RESULT
          WRITEO O           AND OUTPUT IT
          BX6    X6-X6       OUTPUT END OF LINE
          WRITEO O
          EQ     FIB1

 FIB2     WRITER O           WRITE END OF RECORD

          ENDRUN             EXIT PROGRAM

**        WORKING STORAGE

 I1       CON    0
 I2       CON    1
 N        CON    0

**        FET AND OUTPUT BUFFER

 O        BSS    0
 OUTPUT   FILEB  OBUF,OBUFL
 OBUF     BSS    OBUFL

          END    FIB
=====
S\x14
W EXIT TEXT MODE
W^/$
D1000
Smodify,p=0,n=npl,c=0,f,lo=e,z./*create source
D1000
L
W MODIFICATION COMPLETE
W^/$
Sattach,opl=opl871/un=install
D1000
L
W^/$
Smodify,p=npl,lo=e,z./*oplfile opl/*edit fib
D1000
L
W MODIFICATION COMPLETE
W^/$
Scompass,i,l=0
D1000
L
WCPU SECONDS  ASSEMBLY TIME
W^/$
Slgo
D1000
L
WLGO\\.
W^/$
