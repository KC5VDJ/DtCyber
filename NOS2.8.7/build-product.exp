#!/usr/local/bin/expect -f
::tcl::tm::path add .
package require DtCyber;
namespace import DtCyber::*
trap int_handler { SIGINT }

proc int_handler {} {
    error_condition "sigint recieved"
}

set prod [string tolower [lindex $argv 0]]
set f [open "opt/products.idx" r]

if { $prod == "list" } {
    while { 1 } {
        set line [gets $f]
        if { [eof $f] } { exit }
        set tokens [split $line " "]
        if { [lindex $tokens 0] == "*" } {
            send_user "$line\n"
        } else {
            send_user "[lindex $tokens 0] : [join [lrange $tokens 5 end]]\n"
        }
    }
}
while { 1 } {
    set line [gets $f]
    if { [eof $f] } {
        send_user "\nNo such product: $prod\n"
        exit
    }
    set tokens [split $line " "]
    if { $prod == [lindex $tokens 0] } {
        set pre [lindex $tokens 1]
        set post [lindex $tokens 2]
        set lib [lindex $tokens 3]
        set url [lindex $tokens 4]
        set desc [join [lrange $tokens 5 end]]
        break
    }
}
close $f

set DtCyber::printer_file "LP5xx_C12_E5"
set DtCyber::port "6666"
DtCyber::connect 

set jobname [string toupper $prod]
send_user "\nStarting $jobname\n"
send_user "$desc\n"
set filetype ""

if { $url != "*" } {
    send_user "Download tape image ...\n"
    set filename [lindex [split [lindex [split $url "/"] end] "?"] 0]
    set filetype [lindex [split $filename "."] end]
    set tapepath "opt/tapes/$filename"
    system "curl -L -o $tapepath '$url'"

    if { $filetype == "zip" } {
        set dir [pwd]
        cd "opt/tapes"
        system "unzip -o $filename"
        cd $dir
    }

    if { $filetype == "tap" } {
        dsd "\[UNLOAD,51."
        dsd "\[!"
        mount "13,0,1" $tapepath
        sleep 5
    }
}

if { $pre != "*" } {
    send_user "\nRun $jobname PRE-build script\n"
    source "opt/$pre"
}

set jobpath "opt/$prod.job"
if { [file exists $jobpath] } {
    send_user "\nRun $jobname build job\n"
    if { $url != "*" } {
        run_job 12 4 $jobpath $lib 51
    } else {
        run_job 12 4 $jobpath $lib
    }
}

if { $post != "*" } {
    send_user "\nRun $jobname POST-build script\n"
    source "opt/$post"
}

if { $url != "*" && $filetype == "tap" } {
    dsd "\[UNLOAD,51."
    dsd "\[!"
}

send_user "\nBuild of $jobname complete\n"
