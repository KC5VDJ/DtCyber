#!/usr/local/bin/expect -f
::tcl::tm::path add .
package require DtCyber;
namespace import DtCyber::*
trap int_handler { SIGINT }

proc int_handler {} {
    error_condition "sigint recieved"
}

set prod [string tolower [lindex $argv 0]]
set sysedit 0
if { [string tolower [lindex $argv 1]] == "sysedit" } {
    set sysedit 1
}

set f [open "opt/products.idx" r]

if { $prod == "list" } {
    while { 1 } {
        set line [gets $f]
        if { [eof $f] } { exit }
        set tokens [split $line " "]
        if { [lindex $tokens 0] == "*" } {
            send_user "$line\n"
        } else {
            if { [lindex $tokens 0] != "#" } {
                send_user "[format %-7s [lindex $tokens 0]] : [join [lrange $tokens 7 end]]\n"
            }
        }
    }
}
while { 1 } {
    set line [gets $f]
    if { [eof $f] } {
        send_user "\nNo such product: $prod\n"
        exit
    }
    set tokens [split $line " "]
    if { $prod == [lindex $tokens 0] } {
        set pre [lindex $tokens 1]
        set post [lindex $tokens 2]
        set lib [lindex $tokens 3]
        set sysedRecs [lindex $tokens 4]
        set examples [lindex $tokens 5]
        set url [lindex $tokens 6]
        set desc [join [lrange $tokens 7 end]]
        break
    }
}
close $f

set DtCyber::printer_file "LP5xx_C12_E5"
set DtCyber::port "6666"
DtCyber::connect 

set productName [string toupper $prod]
send_user "\nStarting $productName\n"
send_user "$desc\n"
set filetype ""

if { $url != "*" } {
    send_user "Download tape image ...\n"
    set filename [lindex [split [lindex [split $url "/"] end] "?"] 0]
    set filetype [lindex [split $filename "."] end]
    set tapepath "opt/tapes/$filename"
    system "curl -L -o $tapepath '$url'"

    if { $filetype == "zip" } {
        set dir [pwd]
        cd "opt/tapes"
        system "unzip -o $filename"
        cd $dir
    }

    if { $filetype == "tap" } {
        dsd "\[UNLOAD,51."
        dsd "\[!"
        mount "13,0,1" $tapepath
        sleep 5
    }
}

if { $pre != "*" } {
    foreach script [split $pre ","] {
        send_user "\nRun $productName PRE-build script $script\n"
        source "opt/$script"
    }
}

set jobpath "opt/$prod.job"
if { [file exists $jobpath] } {
    send_user "\nRun $productName build job\n"
    if { $url != "*" } {
        run_job 12 4 $jobpath $lib 51
    } else {
        run_job 12 4 $jobpath $lib
    }
}

if { $post != "*" } {
    foreach script [split $post ","] {
        send_user "\nRun $productName POST-build script $script\n"
        source "opt/$script"
    }
}

if { $url != "*" && $filetype == "tap" } {
    dsd "\[UNLOAD,51."
    dsd "\[!"
}

if { $sysedit == 1 || $examples != "*" } {
    if { $sysedRecs != "*" } {
        send_user "\nSYSEDIT $productName into running system\n"
        dsd "IDLE,IAF."
        dis "ATTACH,P=PRODUCT.;GTR,P,LGO,U.$sysedRecs;SYSEDIT,B=LGO." 1
        sleep 10
        dsd "IAF."
    }
    if { $examples != "*" } {
        foreach script [split $examples ","] {
            set filetype [lindex [split $script "."] end]
            if { $filetype == "job" } {
                send_user "\nRun $productName example installation job $script\n"
                run_job 12 4 "examples/$script"
            } else {
                send_user "\nRun $productName example installation script $script\n"
                source "examples/$script"
            }
        }
    }
}

send_user "\nBuild of $productName complete\n"
