IAFMLR
*IDENT IAFMLR  KEJ.
*/
*/        THIS MODSET SUPPORTS THE UMASS MAILER. IT UPDATES
*/        COMMON DECK *COMSREM* TO ADD TABLES TO *IAF*.
*/
*DECK COMSREM
*INSERT   7
 IAFMLR   SPACE  4,10
***       IAFMLR.            MAILER PP SUPPORT TABLES.
*         K. SHORES.         85/12/13.
*                            86/07/07.  CHANGED TO USE *VMNL*
*
*         NOTE - THESE POINTERS ARE ADDED AFTER ALL EXISTING POINTERS
*         SO THAT ONLY PROGRAMS THAT USE THEM HAVE TO BE RE-ASSEMBLED.
*I 86
*
*T VMLR   36/ FWA MAILER       ,24/ LWA+1 MAILER
*T,VMLR   36/ SUPPORT TABLE    ,24/ SUPPORT TABLE
*/
*/        THE FOLLOWING LINE MUST BE CHANGED IF ANY LOCAL IAF TABLES
*/        ARE DEFINED. THE DEFINITION OF *VMLR* SHOULD BE THE LAST IN
*/        THE TABLE TO AVOID RECOMPILING EVERYTHING WHICH REFERENCES
*/        *IAF* POINTERS.
*/
*I NS2474.3           COMSREM.102
 VMLR     BSS    1           MAILER SUPPORT TABLE POINTERS
*EDIT COMSREM
*/
*DECK,IAFEX
*INSERT   818                -- AFTER *RJ ANA* -- 
          RJ     AMT         ALLOCATE MAILER SUPPORT TABLE
*INSERT   1221               -- AFTER END OF *AMS* SUBROUTINE --
 AMT      SPACE  4,10
**        AMT - ALLOCATE MAILER SUPPORT TABLE
*
*         THIS TABLE HAS ONE BYTE (12 BITS) FOR EACH TERMINAL NUMBER. 
*         THE TABLE STARTS AT TN=0, THUS THE FIRST WORD IS FOR TN=0..3.
*         THE FIFTH BYTE CONTAINS THE LETTERS *ML* TO ALLOW THE PP
*         TO DETERMINE THAT IT IS ACCESSING THE CORRECT TABLE.
*
*         ENTRY  (LWAA) = LWA+1 OF MEMORY ALLOCATED.
*                (VMNL) = 60/ MAXIMUM POSSIBLE USER COUNT.
*
*         EXIT   (VMLR) = 36/FWA, 24/LWA+1 OF MAILER SUPPORT TABLE.
*                (LWAA) = LWA+1 OF MAILER SUPPORT TABLE.
*
*                TO *ABT* IF MAILER SUPPORT TABLE OVERFLOW. 
*
  
 AMT      SUBR               ENTRY/EXIT 
          SA1    VMNL        GET TERMINAL COUNT
          SX6    X1+VPST     TERMINAL COUNT + PSEUDO TERMINALS
          AX6    2           DIVIDE BY FOUR (ENTRIES PER WORD)
          SA2    LWAA        GET LWA+1 ALLOCATED CORE
          SB6    X2          SET FWA FOR PRESET
          SX6    X6+1        WORDS IN TABLE IS (NUM/4)+1
          IX6    X6+X2       SET LWA+1
          SX7    X2          SAVE FWA FOR (VMLR)
          LX7    24 
          SA6    A2          REWRITE (LWAA)
          SB7    X6          SAVE LWA+1 FOR LATER 
          BX7    X7+X6       MERGE LWA WITH FWA
          SA7    VMLR        STORE (VMLR)
          SX6    X7-FWA      CHECK FOR OVERFLOW
          SX2    =C* MAILER SUPPORT TABLE OVERFLOW.*
          PL     X6,ABT      IF OVERFLOW
  
*         SET *ML* IDENTIFICATION IN TABLE
  
          SA1    =2RML
          BX6    X1 
 AMT1     SA6    B6 
          SB6    B6+B1
          LT     B6,B7,AMT1  IF NOT COMPLETE
          EQ     AMTX        RETURN
*/        END OF MODSET IAFMLR.
