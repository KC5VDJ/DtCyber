MAILER
*IDENT,MAILER
*/
*/        ADD MAILER SUPPORT TABLE
*/
*/        THIS MOD ADDS A NEW TABLE *VMLR*, MAILER SUPPORT
*/        TABLE, TO IAF. THIS TABLE CONTAINS THE *SILENCE* BIT USED
*/        BY THE *MLR* PP TO DETERMINE IF A TERMINAL DOES NOT WANT
*/        TO BE NOTIFIED OF NEW MAIL.
*/
*DECK,IAFEX
*INSERT   818                -- AFTER *RJ ANA* -- 
          RJ     AMT         ALLOCATE MAILER SUPPORT TABLE
*INSERT   1221               -- AFTER END OF *AMS* SUBROUTINE --
 AMT      SPACE  4,10
**        AMT - ALLOCATE MAILER SUPPORT TABLE
*
*         THIS TABLE HAS ONE BYTE (12 BITS) FOR EACH TERMINAL NUMBER. 
*         THE TABLE STARTS AT TN=0, THUS THE FIRST WORD IS FOR TN=0..3.
*         THE FIFTH BYTE CONTAINS THE LETTERS *ML* TO ALLOW THE PP
*         TO DETERMINE THAT IT IS ACCESSING THE CORRECT TABLE.
*
*         ENTRY  (LWAA) = LWA+1 OF MEMORY ALLOCATED.
*                (VMNL) = 60/ MAXIMUM POSSIBLE USER COUNT.
*
*         EXIT   (VMLR) = 36/FWA, 24/LWA+1 OF MAILER SUPPORT TABLE.
*                (LWAA) = LWA+1 OF MAILER SUPPORT TABLE.
*
*                TO *ABT* IF MAILER SUPPORT TABLE OVERFLOW. 
*
  
 AMT      SUBR               ENTRY/EXIT 
          SA1    VMNL        GET TERMINAL COUNT
          SX6    X1+VPST     TERMINAL COUNT + PSEUDO TERMINALS
          AX6    2           DIVIDE BY FOUR (ENTRIES PER WORD)
          SA2    LWAA        GET LWA+1 ALLOCATED CORE
          SB6    X2          SET FWA FOR PRESET
          SX6    X6+1        WORDS IN TABLE IS (NUM/4)+1
          IX6    X6+X2       SET LWA+1
          SX7    X2          SAVE FWA FOR (VMLR)
          LX7    24 
          SA6    A2          REWRITE (LWAA)
          SB7    X6          SAVE LWA+1 FOR LATER 
          BX7    X7+X6       MERGE LWA WITH FWA
          SA7    VMLR        STORE (VMLR)
          SX6    X7-FWA      CHECK FOR OVERFLOW
          SX2    =C* MAILER SUPPORT TABLE OVERFLOW.*
          PL     X6,ABT      IF OVERFLOW
  
*         SET *ML* IDENTIFICATION IN TABLE
  
          SA1    =2RML
          BX6    X1 
 AMT1     SA6    B6 
          SB6    B6+B1
          LT     B6,B7,AMT1  IF NOT COMPLETE
          EQ     AMTX        RETURN
*/        END OF IAFMLR.
